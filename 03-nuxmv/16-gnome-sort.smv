MODULE main
VAR
    pos: 0..5;
    arr: array 0..4 of 0..9;
DEFINE
    finished := (pos = 5);
    sorted := arr[0] <= arr[1] & arr[1] <= arr[2] & arr[2] <= arr[3] & arr[3] <= arr[4];
INIT
    pos = 0 & arr[0] = 9 & arr[1] = 7 & arr[2] = 5 & arr[3] = 3 & arr[4] = 1
ASSIGN
    next(pos) := case
        pos = 0 | (pos in {1,2,3,4} & arr[pos] >= arr[pos - 1]): pos + 1;
        TRUE                                                   : pos - 1;
    esac;

    next(arr[0]) := case
        pos = 0 | (pos in {1,2,3,4} & arr[pos] >= arr[pos - 1]): arr[0];
        0 = pos - 1                                    : arr[pos];
        TRUE                                           : arr[0];
    esac;

    next(arr[1]) := case
        pos = 0 | (pos in {1,2,3,4} & arr[pos] >= arr[pos - 1]): arr[1];
        1 = pos                                        : arr[pos - 1];
        1 = pos - 1                                    : arr[pos];
        TRUE                                           : arr[1];
    esac;

    next(arr[2]) := case
        pos = 0 | (pos in {1,2,3,4} & arr[pos] >= arr[pos - 1]): arr[2];
        2 = pos                                        : arr[pos - 1];
        2 = pos - 1                                    : arr[pos];
        TRUE                                           : arr[2];
    esac;

    next(arr[3]) := case
        pos = 0 | (pos in {1,2,3,4} & arr[pos] >= arr[pos - 1]): arr[3];
        3 = pos                                        : arr[pos - 1];
        3 = pos - 1                                    : arr[pos];
        TRUE                                           : arr[3];
    esac;

    next(arr[4]) := case
        pos = 0 | (pos in {1,2,3,4} & arr[pos] >= arr[pos - 1]): arr[4];
        4 = pos                                        : arr[pos - 1];
        TRUE                                           : arr[4];
    esac;

LTLSPEC
    F finished;
LTLSPEC
    F G sorted;
LTLSPEC
    sorted V !finished;
