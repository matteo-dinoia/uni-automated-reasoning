MODULE main
VAR
    arr: array 0..4 of 1..5;
    pc: 1..5;
    cont: boolean;
    i: 0..5;
ASSIGN
    init(arr[0]) := 4;
    init(arr[1]) := 1;
    init(arr[2]) := 3;
    init(arr[3]) := 2;
    init(arr[4]) := 5;
    init(pc)    := 1;
    init(cont)  := TRUE;

    next(arr[0]) := case
        pc = 4 & i - 1 = 0:  arr[1];
        TRUE              :  arr[0];
    esac;

    next(arr[1]) := case
        pc = 4 & i     = 1:  arr[0];
        pc = 4 & i - 1 = 1:  arr[2];
        TRUE              :  arr[1];
    esac;

    next(arr[2]) := case
        pc = 4 & i     = 2:  arr[1];
        pc = 4 & i - 1 = 2:  arr[3];
        TRUE              :  arr[2];
    esac;

    next(arr[3]) := case
        pc = 4 & i     = 3:  arr[2];
        pc = 4 & i - 1 = 3:  arr[4];
        TRUE              :  arr[3];
    esac;

    next(arr[4]) := case
        pc = 4 & i     = 4:  arr[3];
        TRUE              :  arr[4];
    esac;

    next(pc) := case
        pc = 1 & cont                           :  2;
        pc = 1                                  :  5;
        pc = 2 & i < 5                          :  3;
        pc = 2                                  :  1;
        pc = 3 & i < 5 & i >= 1
                    & arr[i - 1] > arr[i]       :  4;
        pc = 3                                  :  2;
        pc = 4                                  :  2;
        TRUE                                    :  5;
    esac;

    next(cont) := case
        pc = 1                 : FALSE;
        pc = 4                 : TRUE;
        TRUE                   : cont;
    esac;

    next(i) := case
        pc = 2                 : min(i + 1, 5);
        pc = 1                 : 0;
        TRUE                   : i;
    esac;
DEFINE
    sorted := arr[0] <= arr[1] & arr[1] <= arr[2] & arr[2] <= arr[3] & arr[3] <= arr[4];

LTLSPEC F (pc = 5);
LTLSPEC sorted V pc != 5;
