MODULE main
VAR
    arr: array 0..4 of 1..5;
    swapped: boolean;
    i: 0..5;
    pc: {while, for, if, inside, ended};
ASSIGN
    init(swapped) := TRUE;
    init(pc)      := while;
    init(i)       := 0;

    init(arr[0])  := 4;
    init(arr[1])  := 1;
    init(arr[2])  := 3;
    init(arr[3])  := 2;
    init(arr[4])  := 5;

    next(pc) := case
        pc = while & swapped = TRUE       : for;
        pc = while                        : ended;
        pc = for & i < 5                  : if;
        pc = for                          : while;
        pc = if & i > 0 & i < 5
            & arr[i - 1] > arr[i]         : inside;
        pc = if | pc = inside             : for;
        TRUE                              : ended;
    esac;

    next(swapped) := case
        pc = while                : FALSE;
        pc = inside               : TRUE;
        TRUE                      : swapped;
    esac;

    next(i) := case
        pc = while                : 0;
        pc = for & i < 5          : i + 1;
        TRUE                      : i;
    esac;

    next(arr[0]) := case
        pc = inside & i = 0 + 1                 : arr[0 + 1];
        TRUE                                    : arr[0];
    esac;

    next(arr[1]) := case
        pc = inside & i = 1                     : arr[1 - 1];
        pc = inside & i = 1 + 1                 : arr[1 + 1];
        TRUE                                    : arr[1];
    esac;

    next(arr[2]) := case
        pc = inside & i = 2                     : arr[2 - 1];
        pc = inside & i = 2 + 1                 : arr[2 + 1];
        TRUE                                    : arr[2];
    esac;

    next(arr[3]) := case
        pc = inside & i = 3                     : arr[3 - 1];
        pc = inside & i = 3 + 1                 : arr[3 + 1];
        TRUE                                    : arr[3];
    esac;

    next(arr[4]) := case
        pc = inside & i = 4                     : arr[4 - 1];
        TRUE                                    : arr[4];
    esac;

DEFINE
    sorted := arr[0] <= arr[1] & arr[1] <= arr[2] & arr[2] <= arr[3] & arr[3] <= arr[4];

LTLSPEC F (pc = ended);

LTLSPEC F (pc = ended & sorted)



