MODULE main
VAR
    d1: {left, middle, right};
    d2: {left, middle, right};
    d3: {left, middle, right};
    d4: {left, middle, right};

    move: 1..4;
INIT
    d1 = left & d2 = left & d3 = left & d4 = left;
DEFINE
    clear1 := TRUE;
    clear2 := d1 != d2;
    clear3 := d1 != d3 & d2 != d3;
    clear4 := d1 != d4 & d2 != d4 & d3 != d4;
INVAR
    (!clear1 -> move != 1) &
    (!clear2 -> move != 2) &
    (!clear3 -> move != 3) &
    (!clear4 -> move != 4);
TRANS -- move one
    (move = 1 <-> next(d1) != d1) &
    (move = 2 <-> next(d2) != d2) &
    (move = 3 <-> next(d3) != d3) &
    (move = 4 <-> next(d4) != d4);
TRANS -- move in free
    (move = 2 -> next(d2) != d1) &
    (move = 3 -> (next(d3) != d1 & next(d3) != d2)) &
    (move = 4 -> (next(d4) != d1 & next(d4) != d2 & next(d4) != d3));
INVAR -- to avoid deadlock
    (((clear1 & clear2) | (clear1 & clear3)) -> move != 4) &
    ((clear1 & clear2) -> move != 3);

LTLSPEC
    ! F (d1 = right & d2 = right & d3 = right & d4 = right);
