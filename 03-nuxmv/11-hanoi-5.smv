MODULE main
VAR
    d1: {left, middle, right};
    d2: {left, middle, right};
    d3: {left, middle, right};
    d4: {left, middle, right};
    d5: {left, middle, right};

    move: 1..5;
INIT
    d1 = left & d2 = left & d3 = left & d4 = left & d5 = left;
DEFINE
    clear1 := TRUE;
    clear2 := d1 != d2;
    clear3 := d1 != d3 & d2 != d3;
    clear4 := d1 != d4 & d2 != d4 & d3 != d4;
    clear5 := d1 != d5 & d2 != d5 & d3 != d5 & d4 != d5;
INVAR
    (!clear1 -> move != 1) &
    (!clear2 -> move != 2) &
    (!clear3 -> move != 3) &
    (!clear4 -> move != 4) &
    (!clear5 -> move != 5);
TRANS -- move one
    (move = 1 <-> next(d1) != d1) &
    (move = 2 <-> next(d2) != d2) &
    (move = 3 <-> next(d3) != d3) &
    (move = 4 <-> next(d4) != d4) &
    (move = 5 <-> next(d5) != d5);
TRANS -- move in free
    (move = 2 -> next(d2) != d1) &
    (move = 3 -> (next(d3) != d1 & next(d3) != d2)) &
    (move = 4 -> (next(d4) != d1 & next(d4) != d2 & next(d4) != d3)) &
    (move = 5 -> (next(d5) != d1 & next(d5) != d2 & next(d5) != d3 & next(d5) != d4));
INVAR -- to avoid deadlock
    ((clear1 & clear2) -> move != 3) &
    (((clear1 & clear2) | (clear1 & clear3)) -> move != 4) &
    (((clear1 & clear2) | (clear1 & clear3) | (clear1 & clear4)
        | (clear2 & clear3) | (clear2 & clear4) | (clear3 & clear4)) -> move != 5);

LTLSPEC
    ! F (d1 = right & d2 = right & d3 = right & d4 = right);
